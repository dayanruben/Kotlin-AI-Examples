plugins {
    kotlin("jvm") version "2.4.0"
    kotlin("plugin.spring") version "2.4.0"
    id("org.springframework.boot") version "4.1.0"
    id("io.spring.dependency-management") version "1.1.7"
    id("com.vaadin") version "25.2.1"
}

group = "io.github.devcrocod.example"
version = "0.0.1-SNAPSHOT"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven { url = uri("https://maven.vaadin.com/vaadin-prereleases") }
}

val vaadinVersion = "25.2.1"
val springAiVersion = "2.0.0"
val coroutinesVersion = "1.11.0"

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-reflect")
    implementation(platform("org.springframework.ai:spring-ai-bom:$springAiVersion"))
    implementation(platform("com.vaadin:vaadin-bom:$vaadinVersion"))

    /* ----------------------------- Spring AI ------------------------------ */
    implementation("org.springframework.ai:spring-ai-starter-model-openai")
    /* --------------------------- Vector Stores ---------------------------- */
    implementation("org.springframework.ai:spring-ai-starter-vector-store-chroma")
    implementation("org.springframework.ai:spring-ai-vector-store-advisor")

    /* ------------------------------ Vaadin -------------------------------- */
    implementation("com.vaadin:vaadin-spring-boot-starter")
    implementation("com.vaadin:hilla-spring-boot-starter")
    developmentOnly("com.vaadin:vaadin-dev:$vaadinVersion")

    /* --------------------------- Spring Starters -------------------------- */
    implementation("org.springframework.boot:spring-boot-starter-webflux")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    developmentOnly("org.springframework.boot:spring-boot-devtools")

    /* ---------------------------- Observability --------------------------- */
    implementation("io.micrometer:micrometer-tracing-bridge-brave")
    implementation("io.zipkin.reporter2:zipkin-reporter-brave")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    runtimeOnly("io.micrometer:micrometer-registry-prometheus")

    /* ------------------------------ Logging ------------------------------- */
    implementation("com.github.loki4j:loki-logback-appender:2.1.0")

    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutinesVersion}")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-reactor:${coroutinesVersion}")

    /* ------------------------------ Testing ------------------------------- */
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit5")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll("-Xjsr305=strict")
    }
}

tasks.withType<Test> {
    useJUnitPlatform()
}

vaadin {
    productionMode = project.hasProperty("production")
}

tasks.named<org.springframework.boot.gradle.tasks.run.BootRun>("bootRun") {
    jvmArgs(
        "-Xdebug",
        "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5247"
    )
}

defaultTasks("bootRun")